// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${Versions.gradle}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.kotlin}"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.21.0")
    id("com.github.ben-manes.versions").version("0.42.0")
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    apply from: "$rootDir/ktlint.gradle"
    apply from: "$rootDir/detekt.gradle"

    // Temporary solution to handle the case when `./gradlew connectedAndroidTest` was failing with
    // "D8: MethodHandle.invoke and MethodHandle.invokeExact are only supported starting with
    // Android O (--min-api 26)" error, the issue has been reported in mockk library a while ago:
    // https://github.com/mockk/mockk/issues/281
    configurations.all {
        resolutionStrategy {
            force("org.objenesis:objenesis:2.6")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}